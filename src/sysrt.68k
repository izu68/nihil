; -----------------------------------------------------------------
; Nihil - System Runtime
; Contents :
;		(First stage program init)
;		Clear WRAM & Disable TMSS
;		Copy initialized Non-Const Variables
; -----------------------------------------------------------------

	section .text
	global entry_point

	include "ports.68k"

; ================= WRAM Clear & TMSS Disable =====================

WRAM_BASE = $FFFF0000
WRAM_SIZE = $FFEF

entry_point:
	move.b	(HWVERSION), d7	; Disable TMSS if needed
	and.b   #$0F, d7
    	beq.s   _no_tmss
    	move.l  #'SEGA', (TMSSPORT)
_no_tmss:

	lea	WRAM_BASE,a0	; Clear WRAM
	move.w	#(WRAM_SIZE/(4*4))-1, d7
	moveq	#0,d0
_clr_loop:
	move.l	d0,(a0)+
	move.l	d0,(a0)+
	move.l	d0,(a0)+
	move.l	d0,(a0)+
	dbra	d7,_clr_loop
	;bra	no_data_cpy	; Ignore .data init if not using C

; ================= Copy .data ====================================

	lea	_stext,a0	; Data follows text section
	lea	WRAM_BASE,a1  	; .data is at start of WRAM
	move.w	#_sdata,d0
	beq.s	no_data_cpy
	lsr.w	#1,d0
	subq.w	#1,d0
_cpy_loop:
	move.w	(a0)+,(a1)+
	dbra	d0,_cpy_loop

no_data_cpy:
	moveq	#0,d0		; Clear "main" registers
	move.l	d0,d1
	move.l	d0,d2
	move.l	d0,d3
	move.l	d0,d4
	move.l	d0,d5
	move.l	d0,d6
	move.l	d0,d7
	move.l	d0,a0
	move.l	d0,a1
	move.l	d0,a2
	move.l	d0,a3
	move.l	d0,a4
	move.l	d0,a5
	move.l	d0,a6
	jmp	(main_asm).l
